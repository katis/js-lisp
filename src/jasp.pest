WHITESPACE = _{ " " | "\r" | "\n" | "\t" | "," }
alpha = _{ CASED_LETTER }
digit = _{ '0'..'9' }

integer = @{ (digit)+ }
float = @{ (digit)+ ~ "." ~ (digit)* }

string = ${ "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

symbol = ${ symbol_char ~ (symbol_char | digit)* }
symbol_char = _{ alpha | "-" | "_" | "?" | "." | "+" | "/" | "*" | "=" }

atom = ${ ":" ~ symbol }

expr = { float | integer | string | atom | symbol | list | vector | set | map | quoted }

list = { "(" ~ (expr)* ~ ")" }
vector = { "[" ~ (expr)* ~ "]" }
set = { "#{" ~ (expr)* ~ "}" }
map = { "{" ~ (expr ~ expr)* ~ "}" }
quoted = @{ "'" ~ expr }

module = { SOI ~ (expr)* ~ EOI }
