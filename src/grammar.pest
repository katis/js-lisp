WHITESPACE = _{ " " | "\r" | "\n" | "\t" }
alpha = _{ CASED_LETTER }
digit = { '0'..'9' }

integer = @{ (digit)+ }
float = @{ (digit)+ ~ "." ~ (digit)* }

string = ${ "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

import = _{ "import" }
import_empty = { "[" ~ "]" }
import_vec = { "[" ~ (ident)* ~ "]" }
import_output = _{ import_empty | import_vec | ident }
import_pair = { string ~ import_output }
imports = { "(" ~ import ~ (import_pair)+ ~ ")" }

ident_symbol = { alpha | "-" | "_" }
ident = @{ ident_symbol ~ (ident_symbol | digit)* }

atom = ${ ":" ~ ident }

literal = {  float | integer | string | atom }

list = { "(" ~ (expr)+ ~ (" " ~ expr)* ~ ")" }

expr = _{ ident | literal | list }

file = { SOI ~ (imports)? ~ (list)* ~ EOI }
